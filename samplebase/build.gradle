apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


def releaseVersion = '0.0.1-Alpha'
def groupName = 'tech.thdev'

group = groupName
version = releaseVersion

install {
    repositories.mavenInstaller {
        pom.project {
            name 'BintraySample'
            description 'is sample base'
            url 'https://github.com/taehwandev/BintraySample'
            inceptionYear '2016'

            packaging 'aar'
            // gradle dependency
            // ex) compile '(groupId)tech.thdev:(artifactId)sample-base:(version)0.0.1'
            groupId groupName
            artifactId 'Sample Base'
            version releaseVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/taehwandev/BintraySample.git'
                developerConnection 'https://github.com/taehwandev/BintraySample.git'
                url 'https://github.com/taehwandev/BintraySample'

            }
            developers {
                developer {
                    id 'taehwandev'
                    name 'taehwan'
                    email 'taehwan@thdev.tech'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = groupName // Repository name (Bintray repository name create)
        name = project.name
        userOrg = user
        licenses = ['Apache-2.0']

        vcsUrl = 'https://github.com/taehwandev/BintraySample.git' // git url
        websiteUrl = 'https://github.com/taehwandev/BintraySample' // Website url

        githubRepo = 'taehwandev/BintraySample' // Optional Github repository

        labels = ['aar', 'android', 'example']
        publicDownloadNumbers = true

        version {
            name = releaseVersion // the name of your library project
            desc = 'Gradle Bintray Sample base alpha'
            vcsTag = releaseVersion
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName releaseVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'
}
