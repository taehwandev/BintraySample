apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


def releaseVersion = '0.0.1'
group = 'tech.thdev'
version = releaseVersion

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'gradle-bintray-plugin-sample-base'
        userOrg = user
        licenses = ['Apache-2.0']

        vcsUrl = 'https://github.com/taehwandev/BintraySample.git' // git url
        websiteUrl = 'https://github.com/taehwandev/BintraySample' // Website url

        githubRepo = 'taehwandev/BintraySample' // Optional Github repository

        version {
            name = releaseVersion // the name of your library project
            desc = 'Gradle Bintray Sample base'
            released = new Date()
            vcsTag = releaseVersion
        }
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName releaseVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'
}
